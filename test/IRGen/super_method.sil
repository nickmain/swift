// RUN: rm -rf %t
// RUN: mkdir %t
// RUN: %target-swift-frontend -emit-module -module-name OutsideClasses -o %t %S/../Inputs/outside_classes_before.swift
// RUN: %target-swift-frontend -use-native-super-method -parse-sil -parse-as-library -emit-ir -I %t %s | FileCheck %s --check-prefix=IRGEN
// RUN: %target-swift-frontend -use-native-super-method -force-resilient-super-dispatch -parse-sil -parse-as-library -emit-ir -I %t %s | FileCheck %s --check-prefix=IRGEN-RESILIENT

sil_stage canonical

import Builtin
import Swift
import SwiftShims
import OutsideClasses

public class GrandchildToOutside : OutsideChild {
  public override func method()
   deinit 
  override init()
}

public class GenericGrandchildToOutside<A> : GenericOutsideChild<A> {
  public override func method()
   deinit 
}

public class ConcreteGrandchildToOutside : ConcreteOutsideChild {
  public override func method()
   deinit 
  override init(property: String)
}

// OutsideClasses.OutsideParent.method () -> ()
sil @_TFC14OutsideClasses13OutsideParent6methodfT_T_ : $@convention(method) (@guaranteed OutsideParent) -> ()

// OutsideClasses.OutsideChild.method () -> ()
sil @_TFC14OutsideClasses12OutsideChild6methodfT_T_ : $@convention(method) (@guaranteed OutsideChild) -> ()

sil @GrandchildToOutside_method : $@convention(method) (@guaranteed GrandchildToOutside) -> () {
bb0(%0 : $GrandchildToOutside):
  debug_value %0 : $GrandchildToOutside, let, name "self", argno 1
  strong_retain %0 : $GrandchildToOutside
  %3 = upcast %0 : $GrandchildToOutside to $OutsideChild
  %4 = super_method %0 : $GrandchildToOutside, #OutsideChild.method!1 : OutsideChild -> () -> () , $@convention(method) (@guaranteed OutsideChild) -> ()
  %5 = apply %4(%3) : $@convention(method) (@guaranteed OutsideChild) -> ()
  strong_release %3 : $OutsideChild
  %7 = tuple ()
  return %7 : $()
}

// IRGEN-LABEL: define void @GrandchildToOutside_method(%C12super_method19GrandchildToOutside*)
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @_TMaC14OutsideClasses12OutsideChild()
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses12OutsideChild*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses12OutsideChild*)*, void (%C14OutsideClasses12OutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses12OutsideChild*)*, void (%C14OutsideClasses12OutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN: call void

// IRGEN-RESILIENT-LABEL: define void @GrandchildToOutside_method(%C12super_method19GrandchildToOutside*)
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @_TMaC12super_method19GrandchildToOutside()
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT: [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses12OutsideChild*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses12OutsideChild*)*, void (%C14OutsideClasses12OutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses12OutsideChild*)*, void (%C14OutsideClasses12OutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN-RESILIENT: call void

// OutsideClasses.GenericOutsideParent.method () -> ()
sil @_TFC14OutsideClasses20GenericOutsideParent6methodfT_T_ : $@convention(method) <τ_0_0> (@guaranteed GenericOutsideParent<τ_0_0>) -> ()

// OutsideClasses.GenericOutsideChild.method () -> ()
sil @_TFC14OutsideClasses19GenericOutsideChild6methodfT_T_ : $@convention(method) <τ_0_0> (@guaranteed GenericOutsideChild<τ_0_0>) -> ()

sil @GenericGrandchildToOutside_method : $@convention(method) <A> (@guaranteed GenericGrandchildToOutside<A>) -> () {
bb0(%0 : $GenericGrandchildToOutside<A>):
  debug_value %0 : $GenericGrandchildToOutside<A>, let, name "self", argno 1
  strong_retain %0 : $GenericGrandchildToOutside<A>
  %3 = upcast %0 : $GenericGrandchildToOutside<A> to $GenericOutsideChild<A>
  %4 = super_method %0 : $GenericGrandchildToOutside<A>, #GenericOutsideChild.method!1 : <A> GenericOutsideChild<A> -> () -> () , $@convention(method) <τ_0_0> (@guaranteed GenericOutsideChild<τ_0_0>) -> ()
  %5 = apply %4<A>(%3) : $@convention(method) <τ_0_0> (@guaranteed GenericOutsideChild<τ_0_0>) -> ()
  strong_release %3 : $GenericOutsideChild<A>
  %7 = tuple ()
  return %7 : $()
}

// IRGEN-LABEL: define void @GenericGrandchildToOutside_method
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @swift_getGenericMetadata1
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses19GenericOutsideChild*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses19GenericOutsideChild*)*, void (%C14OutsideClasses19GenericOutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses19GenericOutsideChild*)*, void (%C14OutsideClasses19GenericOutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN: call void

// IRGEN-RESILIENT-LABEL: define void @GenericGrandchildToOutside_method
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @swift_getGenericMetadata1
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT: [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses19GenericOutsideChild*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses19GenericOutsideChild*)*, void (%C14OutsideClasses19GenericOutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses19GenericOutsideChild*)*, void (%C14OutsideClasses19GenericOutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN-RESILIENT: call void

// OutsideClasses.ConcreteOutsideChild.method () -> ()
sil @_TFC14OutsideClasses20ConcreteOutsideChild6methodfT_T_ : $@convention(method) (@guaranteed ConcreteOutsideChild) -> ()

sil @ConcreteGrandchildToOutside_method : $@convention(method) (@guaranteed ConcreteGrandchildToOutside) -> () {
bb0(%0 : $ConcreteGrandchildToOutside):
  debug_value %0 : $ConcreteGrandchildToOutside, let, name "self", argno 1
  strong_retain %0 : $ConcreteGrandchildToOutside
  %3 = upcast %0 : $ConcreteGrandchildToOutside to $ConcreteOutsideChild
  %4 = super_method %0 : $ConcreteGrandchildToOutside, #ConcreteOutsideChild.method!1 : ConcreteOutsideChild -> () -> () , $@convention(method) (@guaranteed ConcreteOutsideChild) -> ()
  %5 = apply %4(%3) : $@convention(method) (@guaranteed ConcreteOutsideChild) -> ()
  strong_release %3 : $ConcreteOutsideChild
  %7 = tuple ()
  return %7 : $()
}

// IRGEN-LABEL: define void @ConcreteGrandchildToOutside_method(%C12super_method27ConcreteGrandchildToOutside*)
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @_TMaC14OutsideClasses20ConcreteOutsideChild()
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses20ConcreteOutsideChild*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses20ConcreteOutsideChild*)*, void (%C14OutsideClasses20ConcreteOutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses20ConcreteOutsideChild*)*, void (%C14OutsideClasses20ConcreteOutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN: call void

// IRGEN-RESILIENT-LABEL: define void @ConcreteGrandchildToOutside_method(%C12super_method27ConcreteGrandchildToOutside*)
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @_TMaC12super_method27ConcreteGrandchildToOutside()
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT: [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%C14OutsideClasses20ConcreteOutsideChild*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%C14OutsideClasses20ConcreteOutsideChild*)*, void (%C14OutsideClasses20ConcreteOutsideChild*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load void (%C14OutsideClasses20ConcreteOutsideChild*)*, void (%C14OutsideClasses20ConcreteOutsideChild*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN-RESILIENT: call void

sil_vtable GrandchildToOutside {
  #OutsideParent.method!1: GrandchildToOutside_method
}

sil_vtable GenericGrandchildToOutside {
  #GenericOutsideParent.method!1: GenericGrandchildToOutside_method
}

sil_vtable ConcreteGrandchildToOutside {
  #GenericOutsideParent.method!1: ConcreteGrandchildToOutside_method
}

