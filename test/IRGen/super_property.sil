// RUN: rm -rf %t
// RUN: mkdir %t
// RUN: %target-swift-frontend -emit-module -module-name OutsideClasses -o %t %S/../Inputs/outside_classes_before.swift
// RUN: %target-swift-frontend -use-native-super-method -parse-sil -parse-as-library -emit-ir -I %t %s | FileCheck %s --check-prefix=IRGEN
// RUN: %target-swift-frontend -use-native-super-method -force-resilient-super-dispatch -parse-sil -parse-as-library -emit-ir -I %t %s | FileCheck %s --check-prefix=IRGEN-RESILIENT

sil_stage canonical

import Builtin
import Swift
import SwiftShims
import OutsideClasses

class GrandchildToOutside : OutsideChild {
  func useProperty()
   deinit 
  override init()
}

class GenericGrandchildToOutside<A> : GenericOutsideChild<A> {
  func useProperty()
   deinit 
}

class ConcreteGrandchildToOutside : ConcreteOutsideChild {
  func useProperty()
   deinit 
  override init(property: String)
}

let c: () -> ()

let cHasGenerics: () -> ()

sil hidden @GrandchildToOutside_useProperty : $@convention(method) (@guaranteed GrandchildToOutside) -> () {
bb0(%0 : $GrandchildToOutside):
  debug_value %0 : $GrandchildToOutside, let, name "self", argno 1
  strong_retain %0 : $GrandchildToOutside
  %3 = upcast %0 : $GrandchildToOutside to $OutsideChild
  %4 = upcast %3 : $OutsideChild to $OutsideParent
  %5 = super_method %0 : $GrandchildToOutside, #OutsideParent.property!getter.1 : OutsideParent -> () -> String , $@convention(method) (@guaranteed OutsideParent) -> @owned String
  %6 = apply %5(%4) : $@convention(method) (@guaranteed OutsideParent) -> @owned String
  strong_release %4 : $OutsideParent
  release_value %6 : $String
  %9 = tuple ()
  return %9 : $()
}

// IRGEN-LABEL: define hidden void @GrandchildToOutside_useProperty(%C14super_property19GrandchildToOutside*)
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @_TMaC14OutsideClasses12OutsideChild()
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)*, { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)*, { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN: call

// IRGEN-RESILIENT-LABEL: define hidden void @GrandchildToOutside_useProperty(%C14super_property19GrandchildToOutside*)
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @_TMaC14super_property19GrandchildToOutside()
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT: [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)*, { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)*, { i8*, i{{(64|32)}}, i{{(64|32)}} } (%C14OutsideClasses13OutsideParent*)** [[VTABLE_SLOT]]
// Some bitcasts
// IRGEN-RESILIENT: call

sil hidden @GenericGrandchildToOutside_useProperty : $@convention(method) <A> (@guaranteed GenericGrandchildToOutside<A>) -> () {
bb0(%0 : $GenericGrandchildToOutside<A>):
  debug_value %0 : $GenericGrandchildToOutside<A>, let, name "self", argno 1
  %2 = alloc_stack $A
  strong_retain %0 : $GenericGrandchildToOutside<A>
  %4 = upcast %0 : $GenericGrandchildToOutside<A> to $GenericOutsideChild<A>
  %5 = upcast %4 : $GenericOutsideChild<A> to $GenericOutsideParent<A>
  %6 = super_method %0 : $GenericGrandchildToOutside<A>, #GenericOutsideParent.property!getter.1 : <A> GenericOutsideParent<A> -> () -> A , $@convention(method) <τ_0_0> (@out τ_0_0, @guaranteed GenericOutsideParent<τ_0_0>) -> ()
  %7 = alloc_stack $A
  %8 = apply %6<A>(%7#1, %5) : $@convention(method) <τ_0_0> (@out τ_0_0, @guaranteed GenericOutsideParent<τ_0_0>) -> ()
  strong_release %5 : $GenericOutsideParent<A>
  copy_addr [take] %7#1 to [initialization] %2#1 : $*A
  dealloc_stack %7#0 : $*@local_storage A
  destroy_addr %2#1 : $*A
  dealloc_stack %2#0 : $*@local_storage A
  %14 = tuple ()
  return %14 : $()
}

// IRGEN-LABEL: define hidden void @GenericGrandchildToOutside_useProperty(%C14super_property26GenericGrandchildToOutside*)
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @swift_getGenericMetadata1
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[VTABLE_SLOT]]
// IRGEN: call

// IRGEN-RESILIENT-LABEL: define hidden void @GenericGrandchildToOutside_useProperty(%C14super_property26GenericGrandchildToOutside*)
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @swift_getGenericMetadata1
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT:  [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[VTABLE_SLOT]]
// IRGEN-RESILIENT: call

sil hidden @ConcreteGrandchildToOutside_useProperty : $@convention(method) (@guaranteed ConcreteGrandchildToOutside) -> () {
bb0(%0 : $ConcreteGrandchildToOutside):
  debug_value %0 : $ConcreteGrandchildToOutside, let, name "self", argno 1
  strong_retain %0 : $ConcreteGrandchildToOutside
  %3 = upcast %0 : $ConcreteGrandchildToOutside to $ConcreteOutsideChild
  %4 = upcast %3 : $ConcreteOutsideChild to $GenericOutsideParent<String>
  %5 = super_method %0 : $ConcreteGrandchildToOutside, #GenericOutsideParent.property!getter.1 : <A> GenericOutsideParent<A> -> () -> A , $@convention(method) <τ_0_0> (@out τ_0_0, @guaranteed GenericOutsideParent<τ_0_0>) -> ()
  %6 = alloc_stack $String
  %7 = apply %5<String>(%6#1, %4) : $@convention(method) <τ_0_0> (@out τ_0_0, @guaranteed GenericOutsideParent<τ_0_0>) -> ()
  strong_release %4 : $GenericOutsideParent<String>
  %9 = load %6#1 : $*String
  dealloc_stack %6#0 : $*@local_storage String
  release_value %9 : $String
  %12 = tuple ()
  return %12 : $()
}

// IRGEN-LABEL: define hidden void @ConcreteGrandchildToOutside_useProperty(%C14super_property27ConcreteGrandchildToOutside*)
// IRGEN: [[SUPER_METADATA:%[0-9]+]] = call %swift.type* @_TMaC14OutsideClasses20ConcreteOutsideChild()
// IRGEN: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)**
// IRGEN: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN: [[FN_PTR:%[0-9]+]] = load void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[VTABLE_SLOT]]
// IRGEN: call

// IRGEN-RESILIENT-LABEL: define hidden void @ConcreteGrandchildToOutside_useProperty(%C14super_property27ConcreteGrandchildToOutside*)
// IRGEN-RESILIENT: [[METADATA:%[0-9]+]] = call %swift.type* @_TMaC14super_property27ConcreteGrandchildToOutside()
// IRGEN-RESILIENT: [[OPAQUE_METADATA:%[0-9]+]] = bitcast %swift.type* [[METADATA]] to %swift.type**
// IRGEN-RESILIENT: [[SUPER_METADATA_PTR:%[0-9]+]] = getelementptr inbounds %swift.type*, %swift.type** [[OPAQUE_METADATA]], i32 1
// IRGEN-RESILIENT: [[SUPER_METADATA:%[0-9]+]] = load %swift.type*, %swift.type** [[SUPER_METADATA_PTR]]
// IRGEN-RESILIENT: [[OPAQUE_SUPER_METADATA:%[0-9]+]] = bitcast %swift.type* [[SUPER_METADATA]] to void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)**
// IRGEN-RESILIENT: [[VTABLE_SLOT:%[0-9]+]] = getelementptr inbounds void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[OPAQUE_SUPER_METADATA]]
// IRGEN-RESILIENT: [[FN_PTR:%[0-9]+]] = load void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)*, void (%swift.opaque*, %C14OutsideClasses20GenericOutsideParent*)** [[VTABLE_SLOT]]
// IRGEN-RESILIENT: call

sil_vtable GrandchildToOutside {
  #GrandchildToOutside.useProperty!1: GrandchildToOutside_useProperty
}

sil_vtable GenericGrandchildToOutside {
  #GenericGrandchildToOutside.useProperty!1: GenericGrandchildToOutside_useProperty
}

sil_vtable ConcreteGrandchildToOutside {
  #ConcreteGrandchildToOutside.useProperty!1: ConcreteGrandchildToOutside_useProperty
}
